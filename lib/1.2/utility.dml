/*
  Â© 2010-2022 Intel Corporation
  SPDX-License-Identifier: 0BSD
*/

dml 1.2;

template _reg_or_field {
    parameter is_register  = ($objtype == "register");
    parameter reg_or_field = ($is_register) ? "register" : "field";
}

/* <add id="dml utilities 1.2">
   <name index="true">read_write</name>
   <doc>
   <doc-item name="DESCRIPTION">
   The object value can be modified by both software and hardware. Uses the
   default read and write methods.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   None.
   </doc-item>
   </doc>
   </add>
*/
template read_write {
    // for info only - read/write have defaults in dml-builtins.dml
}

/* <add id="dml utilities 1.2">
   <name index="true">scratch</name>
   <doc>
   <doc-item name="DESCRIPTION">
   The object is a data scratch area.
   Uses the default read and write methods.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   None.
   </doc-item>
   </doc>
   </add>
*/
template scratch {
    // for info only - read/write
}

/* <add id="dml utilities 1.2">
   <name index="true">ignore_write</name>
   <doc>
   <doc-item name="DESCRIPTION">
   Software writes are ignored. This template might also be useful for read-only
   fields inside an otherwise writable register. See the documentation for the
   read_only template for more information.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   None.
   </doc-item>
   </doc>
   </add>
*/
template ignore_write {
    if ($objtype == "register") {
        method write_access_main(mop, msb1, lsb, value) {
            // ignored
        }
    } else if ($objtype == "field") {
        method write(value) {
            // ignored
        }
    } else {
        error "ignore_write can only be used on registers and fields";
    }
}

/* <add id="dml utilities 1.2">
   <name index="true">read_zero</name>
   <doc>
   <doc-item name="DESCRIPTION">
   Software reads return 0.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   None.
   </doc-item>
   </doc>
   </add>
*/
template read_zero {
    method read -> (value) {
        value = 0;
    }
}

/* <add id="dml utilities 1.2">
   <name index="true">read_only</name>
   <doc>
   <doc-item name="DESCRIPTION">
   The object value is read-only for software, the object value can be
   modified by hardware.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   First software write results in a spec_violation log-message on
   log-level 1, remaining writes on log-level 2. Fields will only log if the
   written value is different from the old value.

   If the register containing the read-only field also contains writable fields,
   it may be better to use the ignore_write template instead, since software
   often do not care about what gets written to a read-only field, causing
   unnecessary logging.
   </doc-item>
   </doc>
   </add>
*/
template read_only {
    is _reg_or_field;

    method write(value) {
        static uint8 log_level = 1;
        if ($is_register) {
            log spec_viol, log_level, Register_Write:
                "Write to read-only %s %s (value written = 0x%0*x).",
                $reg_or_field, $qname, $size * 2, value;
            log_level = 2;
        } else {
            if ($allocate) {
                if (value != $this) {
                    log spec_viol, log_level, Register_Write:
                        "Write to read-only %s %s (value written = 0x%0*x, contents = 0x%0*x).",
                        $reg_or_field, $qname, $size * 2, value, $size * 2, $this;
                    log_level = 2;
                }
            } else {
                local typeof(value) fval;
                inline $get() -> (fval);
                if (value != fval) {
                    log spec_viol, log_level, Register_Write:
                        "Write to read-only %s %s (value written = 0x%0*x, contents = 0x%0*x).",
                        $reg_or_field, $qname, $size * 2, value, $size * 2, fval;
                    log_level = 2;
                }
            }
        }
    }
    if ($is_register) {
        method write_access_main(mop, msb1, lsb, value) {
            inline $write(value);
        }
    }
}

/* <add id="dml utilities 1.2">
   <name index="true">write_only</name>
   <doc>
   <doc-item name="DESCRIPTION">
   The objects value can be modified by software but can't be read back, reads
   return 0.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   The first time the object is read there is a spec_violation
   log-message on log-level 1, remaining reads on log-level 2.
   </doc-item>
   </doc>
   </add>
*/
template write_only {
    is _reg_or_field;
    method read() -> (value) {
        if ($is_register) {
            static uint8 log_level = 1;
            log spec_viol, log_level, Register_Read:
                "Read from write-only register %s (returning 0).", $qname;
            log_level = 2;
        } else {
            _warning "The write_only template only makes sense on registers."
                + " For fields, use read_zero instead.";
        }
        value = 0;
    }
    if ($is_register) {
        method read_access_main(mop, lsb, msb1) -> (value) {
            inline $read() -> (value);
        }
    }
}

/* <add id="dml utilities 1.2">
   <name index="true">write_1_clears</name>
   <doc>
   <doc-item name="DESCRIPTION">
   Software can only clear bits. This feature is often when
   hardware sets bits and software clears them to acknowledge.
   Software write 1's to clear bits. The new object value is
   a bitwise AND of the old object value and the bitwise
   complement of the value written by software.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   None.
   </doc-item>
   </doc>
   </add>
*/
template write_1_clears {
    method write(value) {
        $this = $this & ~value;
    }
}

/* <add id="dml utilities 1.2">
   <name index="true">clear_on_read</name>
   <doc>
   <doc-item name="DESCRIPTION">
   Software reads return the object value. The object value is then reset to 0
   as a side-effect of the read.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   None.
   </doc-item>
   </doc>
   </add>
*/
template clear_on_read {
    method read() -> (value) {
        value = $this;
        $this = 0;
    }
}

/* <add id="dml utilities 1.2">
   <name index="true">write_1_only</name>
   <doc>
   <doc-item name="DESCRIPTION">
   Software can only set bits to 1.
   The new object value is the bitwise OR of the
   old object value and the value written by software.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   None.
   </doc-item>
   <doc-item name="RELATED TEMPLATES">write_0_only</doc-item>
   </doc>
   </add>
*/
template write_1_only {
    method write(value) {
        $this |= value;
    }
}

/* <add id="dml utilities 1.2">
   <name index="true">write_0_only</name>
   <doc>
   <doc-item name="DESCRIPTION">
   Software can only set bits to 0.
   The new object value is the bitwise AND of the
   old object value and the value written by software.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   None.
   </doc-item>
   <doc-item name="RELATED TEMPLATES">write_1_only</doc-item>
   </doc>
   </add>
*/
template write_0_only {
    method write(value) {
        $this &= value;
    }
}

/* <add id="dml utilities 1.2">
   <name index="true">read_constant</name>
   <doc>
   <doc-item name="DESCRIPTION">
   Software reads return a constant value. No storage is
   allocated for the object value.  If this template is used on a register,
   it will not be checkpointed.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   None.
   </doc-item>
   <doc-item name="PARAMETERS">
   value: the constant value

   read_val: can be set instead of <param>value</param>, for forward
   compatibility with DML 1.4

   hard_reset_value: if <param>value</param> is <tt>undefined</tt>, this
   parameter is used instead
   </doc-item>
   <doc-item name="RELATED TEMPLATES">constant, silent_constant</doc-item>
   </doc>
   </add>
*/
template read_constant {
    parameter allocate = false;
    parameter configuration = "pseudo";
    parameter value default undefined;
    parameter read_val default $value;
    parameter _real_value =
            ((defined $read_val) ? $read_val :
             ((defined $hard_reset_value) ? $hard_reset_value :
              ((defined $reg.hard_reset_value)
               ? ($reg.hard_reset_value)[$msb:$lsb] : 0)));

    method read() -> (value) {
        value = $_real_value[$bitsize-1:0];
    }
    method get() -> (value) {
        value = $_real_value[$bitsize-1:0];
    }
}

template _generic_constant is (read_constant, _init_val) {
    // in 1.4, read_val cannot be overridden in other
    // templates than read_constant
    parameter read_val = $value;

    method set(value) {
        if (value != $_real_value[$bitsize-1:0]) {
            if (SIM_object_is_configured($dev.obj)) {
                log error: "Can't set %s (attribute value = %#x, expected constant value = %#x)",
                    $qname, value, $_real_value[$bitsize-1:0];
                throw;
            } else {
                log info, 2, 0: "Ignoring value when setting %s (attribute value = %#x, expected constant value = %#x)",
                    $qname, value, $_real_value[$bitsize-1:0];
            }
        }
    }
}

/* <add id="dml utilities 1.2">
   <name index="true">constant</name>
   <doc>
   <doc-item name="DESCRIPTION">
   The object value will remain constant.
   Software and hardware writes does not update the object value.
   If this template is used on a register, it will not be checkpointed.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   First software write to register or field (if field value is not
   equal to write value) results in a spec_violation log-message on
   log-level 1, remaining writes on log-level 2.
   </doc-item>
   <doc-item name="PARAMETERS">
   value: the constant value

   init_val: can be set instead of <param>value</param>, for forward
   compatibility with DML 1.4

   hard_reset_value: if <param>value</param> is <tt>undefined</tt>, this
   parameter is used instead
   </doc-item>
   <doc-item name="RELATED TEMPLATES">read_constant, silent_constant</doc-item>
   </doc>
   </add>
*/
template constant {
    is _generic_constant;
    is _reg_or_field;

    method write(value) {
        static uint8 log_level = 1;
        if ($is_register || value != $_real_value[$bitsize-1:0]) {
            log spec_viol, log_level, Register_Write:
                "Write to constant %s %s (value written = %#x, contents = %#x).",
                $reg_or_field, $qname, value, $_real_value[$bitsize-1:0];
            log_level = 2;
        }
    }
}

/* <add id="dml utilities 1.2">
   <name index="true">silent_constant</name>
   <doc>
   <doc-item name="DESCRIPTION">
   The object value will remain constant.
   Software and hardware writes does not update the initial object value.
   If this template is used on a register, it will not be checkpointed.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   None.
   </doc-item>
   <doc-item name="PARAMETERS">
   value: the constant value

   init_val: can be set instead of <param>value</param>, for forward
   compatibility with DML 1.4

   hard_reset_value: if <param>value</param> is <tt>undefined</tt>, this
   parameter is used instead
   </doc-item>
   <doc-item name="RELATED TEMPLATES">constant, read_constant</doc-item>
   </doc>
   </add>
*/
template silent_constant {
    is _generic_constant;
    is ignore_write;
}

/* <add id="dml utilities 1.2">
   <name index="true">zeros</name>
   <doc>
   <doc-item name="DESCRIPTION">
   The object is constant all 0's.
   Software and hardware writes does not update the object value. The
   object value is all 0's.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   First software write to register or field (if field value is not
   equal to write value) results in a spec_violation log-message on
   log-level 1, remaining writes on log-level 2.
   </doc-item>
   </doc>
   </add>
*/
template zeros {
    is constant;
    parameter value = 0;
}

/* <add id="dml utilities 1.2">
   <name index="true">ones</name>
   <doc>
   <doc-item name="DESCRIPTION">
   The object is constant all 1's.
   Software and hardware writes does not update the object value. The
   object value is all 1's.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   First software write to register or field (if field value is not
   equal to write value) results in a spec_violation log-message on
   log-level 1, remaining writes on log-level 2.
   </doc-item>
   </doc>
   </add>
*/
template ones {
    is constant;
    parameter value = 0xffffffffffffffff;
}

/* <add id="dml utilities 1.2">
   <name index="true">ignore</name>
   <doc>
   <doc-item name="DESCRIPTION">
   The objects functionality is unimportant for both hardware and software.
   Software and hardware reads return 0. Software and hardware writes
   are ignored.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   None.
   </doc-item>
   </doc>
   </add>
*/
template ignore {
    parameter allocate = false;
    is read_zero;
    is ignore_write;
    parameter configuration = "none";
    method get() -> (value) {
        value = 0;
    }
    method set(value) {
        // ignored
    }
}

/* <add id="dml utilities 1.2">
   <name index="true">reserved</name>
   <doc>
   <doc-item name="DESCRIPTION">
   The object is marked reserved and should not be used by software.
   Software writes update the object value. Reads are not affected.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   First software write to register or field (if field value is not
   equal to write value) results in a spec_violation log-message on
   log-level 2. No logs on subsequent writes.
   </doc-item>
   </doc>
   </add>
*/
template reserved {
    is _reg_or_field;
    method write(value) {
        static uint8 log_level = 2;
        if (($is_register || value != $this) && log_level == 2) {
            log spec_viol, log_level, Register_Write:
                "Write to reserved %s %s (value written = %#x, contents = %#x), will not warn again.",
                $reg_or_field, $qname, value, $this;
            log_level = 5;
        }
        $this = value;
    }
}

template _unimpl_base {
    is _reg_or_field;
    parameter limitations = $limits;
}

template _unimpl_base_read_warn {
    if ($is_register) {
        method read_access_main(mop, lsb, msb1) -> (typeof $this value) {
            static uint8 log_level = $log_level_high;
            local typeof(value) current;
            inline $get() -> (current);
            log unimpl, log_level, Register_Read:
                "Read from unimplemented register %s (0x%0*x) (contents = 0x%0*x).",
                $qname, $size * 2, (defined $offset) ? $offset : 0, $size * 2, current;
            log_level = $log_level_low;
            inline default(mop, lsb, msb1) -> (value);
        }
    }
}

template _unimpl_base_write_warn {
    if ($is_register) {
        method write_access_main(mop, msb1, lsb, value) {
            static uint8 log_level = $log_level_high;
            log unimpl, log_level, Register_Write:
                "Write to unimplemented register %s (%#x) " +
                    "(value written = 0x%0*x).",
                    $qname, defined $offset ? $offset : 0, $size * 2, value;
            log_level = $log_level_low;
            inline default(mop, msb1, lsb, value);
        }
    } else {
        method write(value) {
            static uint8 log_level = $log_level_high;
            if (value != $this) {
                log unimpl, log_level, Register_Write:
                    "Write to unimplemented field %s (%#x)"
                    + " (value written = 0x%0*x, contents = 0x%0*x),"
                    + " will not warn again.",
                    $qname, defined $reg.offset ? $reg.offset : 0,
                    $size * 2, value, $size * 2, $this;
                log_level = $log_level_low;
            }
            $this = value;
            if (defined ($reg.limitations)
                && $reg.limitations == "Not implemented.")
                _warning "unimplemented template used in both field and register";
        }
    }
}

template _unimpl_base_warn {
    is _unimpl_base;

    parameter log_level_high default 1;
    parameter log_level_low  default 2;
}

/* <add id="dml utilities 1.2">
   <name index="true">unimplemented</name>
   <doc>
   <doc-item name="DESCRIPTION">
   The object functionality is unimplemented. Warn when software is using the
   object. Software and hardware writes and reads are implemented as default
   writes and reads.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   First software read to a register results in an unimplemented log-message on
   log-level 1, remaining reads on log-level 2. Software reads to fields does
   not result in a log-message. First software write to registers results in
   an unimplemented log-message on log-level 1, remaining writes on log-level 2.
   First write to a field (if field value is not equal to write value) results
   in an unimplemented log-message on log-level 1, remaining writes on
   log-level 2.
   </doc-item>
   <doc-item name="PARAMETERS">
   log_level_high: sets the high log-level (default is 1, see LOG-OUTPUT)

   log_level_low: sets the low log-level (default is 2, see LOG-OUTPUT)
   </doc-item>
   <doc-item name="RELATED TEMPLATES">_read_unimplemented</doc-item>
   <doc-item name="RELATED TEMPLATES">_write_unimplemented</doc-item>
   <doc-item name="RELATED TEMPLATES">silent_unimplemented</doc-item>
   <doc-item name="RELATED TEMPLATES">design_limitation</doc-item>
   </doc>
   </add>
*/
template unimplemented {
    is _unimpl_base_warn;
    is _unimpl_base_read_warn;
    is _unimpl_base_write_warn;
    parameter limits default "Not implemented.";
}

/* <add id="dml utilities 1.2">
   <name index="true">_read_unimplemented</name>
   <doc>
   <doc-item name="DESCRIPTION">
   The object functionality associated to a read access is unimplemented. Write
   access is using default implementation and can be overridden (for instance
   by the read_only template).
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   First software read to a register results in an unimplemented log-message on
   log-level 1, remaining reads on log-level 2. Software reads to fields does
   not result in a log-message.
   </doc-item>
   <doc-item name="PARAMETERS">
   log_level_high: sets the high log-level (default is 1, see LOG-OUTPUT)

   log_level_low: sets the low log-level (default is 2, see LOG-OUTPUT)
   </doc-item>
   <doc-item name="RELATED TEMPLATES">unimplemented</doc-item>
   <doc-item name="RELATED TEMPLATES">_write_unimplemented</doc-item>
   <doc-item name="RELATED TEMPLATES">silent_unimplemented</doc-item>
   <doc-item name="RELATED TEMPLATES">design_limitation</doc-item>
   </doc>
   </add>
*/
template _read_unimplemented {
    is _unimpl_base_warn;
    is _unimpl_base_read_warn;
    parameter limits default "Read-access not implemented.";
}

/* <add id="dml utilities 1.2">
   <name index="true">_write_unimplemented</name>
   <doc>
   <doc-item name="DESCRIPTION">
   The object functionality associated to a write access is unimplemented. Read
   access is using default implementation and can be overridden (for instance
   by the write_only template).
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   First software write to registers results in an unimplemented log-message on
   log-level 1, remaining writes on log-level 2.  First write to a field (if
   field value is not equal to write value) results in an unimplemented
   log-message on log-level 1, remaining writes on log-level 2.
   </doc-item>
   <doc-item name="PARAMETERS">
   log_level_high: sets the high log-level (default is 1, see LOG-OUTPUT)

   log_level_low: sets the low log-level (default is 2, see LOG-OUTPUT)
   </doc-item>
   <doc-item name="RELATED TEMPLATES">unimplemented</doc-item>
   <doc-item name="RELATED TEMPLATES">_read_unimplemented</doc-item>
   <doc-item name="RELATED TEMPLATES">silent_unimplemented</doc-item>
   <doc-item name="RELATED TEMPLATES">design_limitation</doc-item>
   </doc>
   </add>
*/
template _write_unimplemented {
    is _unimpl_base_warn;
    is _unimpl_base_write_warn;
    parameter limits default "Write-access not implemented.";
}

/* <add id="dml utilities 1.2">
   <name index="true">silent_unimplemented</name>
   <doc>
   <doc-item name="DESCRIPTION">
   The object functionality is unimplemented, but do not print a
   lot of log-messages when reading or writing.
   Software and hardware writes and reads are implemented as default writes and
   reads.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   First software read to a register results in an unimplemented log-message on
   log-level 3, remaining reads on log-level 4. Software reads to fields does
   not result in a log-message. First software write to a register results in
   an unimplemented log-message on log-level 3, remaining writes on log-level 4.
   First write to a field (if field value is not equal to write value) results
   in an unimplemented log-message on log-level 3, remaining writes on
   log-level 4.
   </doc-item>
   <doc-item name="RELATED TEMPLATES">unimplemented</doc-item>
   <doc-item name="RELATED TEMPLATES">design_limitation</doc-item>
   </doc>
   </add>
*/
template silent_unimplemented {
    parameter log_level_high = 3;
    parameter log_level_low  = 4;
    is unimplemented;
}

/* <add id="dml utilities 1.2">
   <name index="true">undocumented</name>
   <doc>
   <doc-item name="DESCRIPTION">
   The object functionality is undocumented or poorly documented.
   Software and hardware writes and reads are implemented as default writes and
   reads.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   First software write and read result in a spec_violation log-message on
   log-level 1, remaining on log-level 2.
   </doc-item>
   </doc>
   </add>
*/
template undocumented {
    is _reg_or_field;
    method read() -> (value)  {
        static uint8 log_level = 1;
        log spec_viol, log_level, Register_Read:
            "Read from poorly or non-documented %s %s (contents = %#x).",
            $reg_or_field, $qname, $this;
        log_level = 2;
        value = $this;
    }
    method write(value) {
        static uint8 log_level = 1;
        log spec_viol, log_level, Register_Write:
            "Write to poorly or non-documented %s %s (value written = %#x, contents = %#x).",
            $reg_or_field, $qname, value, $this;
        log_level = 2;
        $this = value;
    }
}

/* <add id="dml utilities 1.2">
   <name index="true">signed</name>
   <doc>
   <doc-item name="DESCRIPTION">
   Defines the object value to be treated as a signed integer, i.e.
   sets the object's <i>signed</i> parameter to <tt>true</tt>.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   None.
   </doc-item>
   </doc>
   </add>
*/
template signed {
    parameter signed = true;
}

/* <add id="dml utilities 1.2">
   <name index="true">noalloc</name>
   <doc>
   <doc-item name="DESCRIPTION">
   Do not allocate any storage for the object value, i.e.
   sets the object's <i>allocate</i> parameter to <tt>false</tt>.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   None.
   </doc-item>
   </doc>
   </add>
*/
template noalloc {
    parameter allocate = false;
}

/* <add id="dml utilities 1.2">
   <name index="true">unmapped</name>
   <doc>
   <doc-item name="DESCRIPTION">
   Make the offset of a register undefined.  This means that it is not mapped
   in the address space of the bank it is in.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   None.
   </doc-item>
   </doc>
   </add>
*/
template unmapped {
    parameter offset = undefined;
}

/* <add id="dml utilities 1.2">
   <name index="true">sticky</name>
   <doc>
   <doc-item name="DESCRIPTION">
   Do not reset object value on soft-reset, keep current value.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   None.
   </doc-item>
   </doc>
   </add>
*/
template sticky {
    method soft_reset() {
        // do nothing
    }
}

/* <add id="dml utilities 1.2">
   <name index="true">checkreserved</name>
   <doc>
   <doc-item name="DESCRIPTION">
   Check if the value written to a register matches the bits in the
   <tt>reserved</tt> parameter, and log a spec_violation when it does.
   </doc-item>
   <doc-item name="LOG-OUTPUT">
   A spec_violation message.
   </doc-item>
   </doc>
   </add>
*/
template checkreserved {
    is _reg_or_field;
    parameter reserved; // An integer
    if ($is_register) {
        method before_write(memop, msb1, lsb, value) {
            if ($reserved != 0) {
                if (((defined lsb ? (value << lsb) : value) & $reserved) != 0) {
                    log spec_viol:
                        "Write to reserved bits in register %s", $qname;
                }
            }
        }
    } else {
        error "checkreserved template can only be applied to registers.";
    }
}

/* <add id="dml utilities 1.2">
   <name index="true">design_limitation</name>
   <doc>
   <doc-item name="DESCRIPTION">
   The object's functionality is not in the model's scope and has been
   left unimplemented as a design decision. Software and hardware
   writes and reads are implemented as default writes and reads.
   Debug registers are a prime example of when to use this
   template. This is different from <i>unimplemented</i> which is
   intended to be implement (if required) but is a limitation in
   the current model.
   </doc-item>
   <doc-item name="RELATED TEMPLATES">unimplemented</doc-item>
   <doc-item name="RELATED TEMPLATES">silent_unimplemented</doc-item>
   </doc>
   </add>
*/
template design_limitation {
    is _unimpl_base;
    parameter limits default ("Not implemented (design limitation)."
                              + " This " + $reg_or_field + " is a dummy "
                              + $reg_or_field + " with no side effects.");
}

/* <add id="dml utilities 1.2">
   <name index="true">no_reset</name>
   <doc>
   <doc-item name="DESCRIPTION">
   The register's or field's value will not be changed on a hard or soft reset.
   </doc-item>
   </doc>
   </add>
*/
template no_reset {
    method hard_reset() {
        if ($allocate) {
            if (!SIM_object_is_configured($dev.obj))
                if (defined $hard_reset_value) {
                    /* Register, or field with a defined hard-reset-value */
                    $this = $hard_reset_value;
                } else {
                    /* Only fields can have an undefined hard-reset-value */
                    $this = ($reg.hard_reset_value)[$msb:$lsb,le];
                }
        }
    }
    method soft_reset() { }
}


// SIM API functions and stuff


extern size_t strlen(const char *s);
extern int memcmp(const void *s1, const void *s2, size_t n);
extern void *memcpy(void *dest, const void *src, size_t n);
extern void *memmove(void *dest, const void *src, size_t n);
