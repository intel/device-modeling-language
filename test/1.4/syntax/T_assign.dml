/*
  Â© 2021-2023 Intel Corporation
  SPDX-License-Identifier: MPL-2.0
*/
dml 1.4;

device test;

import "testing.dml";

method test() throws {
    local int i;

    // right-to-left evaluation of assignment chain
    local int a[4];
    local int b[4];
    i = 0;
    a[++i] = b[++i] = ++i;
    assert b[2] == 1 && a[3] == 1;

    // truncation of values on multi-assignment
    local int8 byte;
    local float f;
    f = byte = i = 255.1;
    assert i == 255 && byte == -1 && f == -1;

    i = 3;

    // local variable is visible inside entire for expression, but not after
    local int sum = 0;
    for (local int i = 1; i < 10; i++) {
        sum += i;
    }
    assert sum == 45 && i == 3;

    // pre-statement order
    local int j;
    for (j = 0, j += 2; j != 2; ) assert false;
    assert j == 2;

    for (i = 18; i != 2; --i, i = 2) {
        // if post-statements are not run correctly, a second failing iteration
        // will be reached
        assert i == 18;
        i = 12;
        continue;
    }

    // assignment chain in for-pre
    for (i = j = 3; false;) ;
    assert i == 3 && j == 3;

    // += operands evaluated twice
    local int c[3] = {0, 0, 0};
    j = 0;
    i = 3;
    c[++j] += ++i;
    assert c[1] == 4 && j == 1 && i == 4;

    // multiple simultaneous assignment
    i = 1;
    j = 0;
    (i, j) = (j, i);
    assert i == 0 && j == 1;
}
