/*
  Â© 2021-2024 Intel Corporation
  SPDX-License-Identifier: MPL-2.0
*/
dml 1.4;
device test;

typedef layout "big-endian" {
    uint24 m;
    uint56 a[2];
    bitfields 16 {
        uint8 m @ [11:4];
    } b;
} be_l;

typedef layout "little-endian" {
    uint24 m;
    uint56 a[2];
    bitfields 16 {
        uint8 m @ [11:4];
    } b;
} le_l;

method betest()
{
    local be_l b;
    b.m = 10;
    b.a[0] = 11;
    b.a[1] = 12;
    b.b.m = 0x7f;

    local uint24_be_t *bm = &b.m;
    local uint56_be_t *ba0 = &b.a[0];
    local uint56_be_t *ba1 = &b.a[1];
    local uint16_be_t *bb = &b.b;

    // Test reading
    assert *bm == 10;
    assert *ba0 == 11;
    assert *ba1 == 12;
    assert *bb == 0x07f0;

    // Test writing
    *bm = 1;
    *ba0 = 2;
    *ba1 = 3;
    *bb = 0xff00;
    assert b.m == 1;
    assert b.a[0] == 2;
    assert b.a[1] == 3;
    assert b.b.m == 0xf0;
}

method letest()
{
    local le_l l;
    l.m = 10;
    l.a[0] = 11;
    l.a[1] = 12;
    l.b.m = 0x7f;

    local uint24_le_t *lm = &l.m;
    local uint56_le_t *la0 = &l.a[0];
    local uint56_le_t *la1 = &l.a[1];
    local uint16_le_t *lb = &l.b;

    // Test reading
    assert *lm == 10;
    assert *la0 == 11;
    assert *la1 == 12;
    assert *lb == 0x07f0;

    // Test writing
    *lm = 1;
    *la0 = 2;
    *la1 = 3;
    *lb = 0xff00;
    assert l.m == 1;
    assert l.a[0] == 2;
    assert l.a[1] == 3;
    assert l.b.m == 0xf0;
}

method init()
{
    betest();
    letest();
}
