/*
  Â© 2021 Intel Corporation
  SPDX-License-Identifier: MPL-2.0
*/
dml 1.4;

device test;

/// DMLC-FLAG --enable-features-for-internal-testing-dont-use-this

import "utility.dml";

// Simple types
saved int saved_int = 2;

saved int24 saved_int24 = -5;

saved bool saved_bool = true;

saved float saved_float = 0.5;

saved size_t saved_size = 2;

saved long saved_long = -5;

// Storage types
typedef struct {
    int i;
} inner_i_t;
// This checks that we do not get duplicate serialization functions from
// nested types
typedef struct {
    int i;
    int a[2];
    inner_i_t inner1;
    inner_i_t inner2;
} i_ii_t;

saved struct { i_ii_t v0; int i; } saved_struct;

saved int saved_array[4];
saved int saved_nested_array[4][3];

// This declaration only exists to test that de/serialization functions for
// large (nested) arrays are generated efficiently, and do not hang dmlc/gcc
saved int64 saved_big_array[8][8192];

// The final dimension of uint8 array types -- both implicit and explicit
// endian -- is serialized as data, and can be deserialized either through data
// or an integer list.
saved uint8 saved_byte_array_via_data[4];
saved uint8_be_t saved_nested_byte_array_via_data[4][3];
saved uint8 saved_byte_array_via_list[4];
saved uint8_be_t saved_nested_byte_array_via_list[4][3];

// The final dimension of int8 array types -- both implicit and explicit
// endian -- is serialized as an integer list, and can be deserialized either
// through data or an integer list
saved char saved_signed_byte_array_via_data[4];
saved int8_be_t saved_nested_signed_byte_array_via_data[4][3];
saved char saved_signed_byte_array_via_list[4];
saved int8_be_t saved_nested_signed_byte_array_via_list[4][3];

// The unused sessions in these objects will complicate the strides of the
// struct arrays in the device
port port_array[i < 4] {
    saved int v = i;
    session int s = i;
    group array_saved_int[j < 3] {
        saved int v = (i+1) * 3 + j;
        session int s = (i+1) * 3 + j;
    }
}

bank b[i < 4] {
    session int filler;
    register r size 4 is unmapped {
        session int filler;
        field f[j < 3] @ [j] {
            saved int v = i * 3 + j ;
        }
    }
}

group g {
    subdevice s {
        port p {
            saved int saved_deep;
        }
    }
}

// TODO: vect

saved int24_le_t saved_int24_le = -8;
saved uint48_be_t saved_uint48_be = 3;

saved bitfields 24 {
    uint20 c @ [23:4];
} saved_bitfields;

typedef layout "little-endian" {
    uint56 i;
} inner_layout_t;

saved layout "big-endian" {
    int32 i32;
    inner_layout_t il;
    bitfields 24 {
        uint24 f @ [23:0];
    } b24;
} saved_layout;

// saved from template
template saved_template {
    param initial_value default 0;
    saved int v = initial_value;
}
port p_saved1 is saved_template;
port p_saved2 is saved_template {
    param initial_value = 4;
}

template t {
    hook() h1;
    hook(int, bool) h2;
    /// WARNING WEXPERIMENTAL
    hook(int, bool) h3[4][2];
    param p : int64;
}

// identity
template mass_groups {
    param coeff;
    group g1 is t { param p = coeff * 1; }
    group g2[i < 3] is t {
        param p = coeff * (2 + i);
        group child is t { param p = coeff * (5 + i); }
        group children[j < 5][l < 7] is t {
            param p = coeff * (8 + 5*7*i + 7*j + l);
        }
    }
}

group initial is mass_groups {
    param coeff = 1;
}

is mass_groups;
param coeff = -1;

saved object saved_identity_g1 = cast(initial.g1, object);
saved object saved_identity_g2_2 = cast(initial.g2[2], object);
saved object saved_identity_g2_0_child =
    cast(initial.g2[0].child, object);
saved object saved_identity_g2_1_children_2_4 =
    cast(initial.g2[1].children[2][4], object);

// test serialization of the device object and a top-level object of the same
// name
group test;

saved object saved_identity_device = cast(dev, object);
saved object saved_identity_test = cast(test, object);

// template types
saved t saved_traitref_g1 = cast(initial.g1, t);
saved t saved_traitref_g2_2 = cast(initial.g2[2], t);
saved t saved_traitref_g2_0_child =
    cast(initial.g2[0].child, t);
saved t saved_traitref_g2_1_children_2_4 =
    cast(initial.g2[1].children[2][4], t);

// Check that zero-initialized trait references don't result in unrestorable
// checkpoints.
saved t saved_traitref_invalid;

// hook references
saved hook() saved_hookref_g1_h1 = initial.g1.h1;
saved hook() saved_hookref_g2_2_h1 = initial.g2[2].h1;
saved hook() saved_hookref_g2_0_child_h1 = initial.g2[0].child.h1;
saved hook() saved_hookref_g2_1_children_2_4_h1 =
    initial.g2[1].children[2][4].h1;

saved hook(int, bool) saved_hookref_g1_h2 = initial.g1.h2;
saved hook(int, bool) saved_hookref_g2_2_h2 = initial.g2[2].h2;
saved hook(int, bool) saved_hookref_g2_0_child_h2 = initial.g2[0].child.h2;
saved hook(int, bool) saved_hookref_g2_1_children_2_4_h2 =
    initial.g2[1].children[2][4].h2;

saved hook(int, bool) saved_hookref_g1_h3_2_1 = initial.g1.h3[2][1];
saved hook(int, bool) saved_hookref_g2_2_h3_2_1 = initial.g2[2].h3[2][1];
saved hook(int, bool) saved_hookref_g2_0_child_h3_2_1 =
    initial.g2[0].child.h3[2][1];
saved hook(int, bool) saved_hookref_g2_1_children_2_4_h3_2_1 =
    initial.g2[1].children[2][4].h3[2][1];

// Check that zero-initialized hook references don't result in unrestorable
// checkpoints, and that hook reference types not otherwise used don't cause
// issues
saved hook()                 saved_hookref_invalid_1;
saved hook(int, bool)        saved_hookref_invalid_2;
saved hook(int, bool, float) saved_hookref_invalid_3;

attribute test_initial is write_only_attr {
    param type = "n";

    method set(attr_value_t _) throws {
        assert saved_int == 2;
        assert saved_int24 == -5;
        assert saved_bool == true;
        assert saved_float == 0.5;
        assert saved_size == 2;
        assert saved_long == -5;
        // No initial test for the struct or array
        for (local int i = 0; i < 4; i++) {
            assert port_array[i].v == i;
            for (local int j = 0; j < 3; j++) {
                assert port_array[i].array_saved_int[j].v == (i + 1) * 3 + j;
                assert b[i].r.f[j].v == i * 3 + j;
            }
        }
        assert saved_int24_le == -8;
        assert saved_uint48_be == 3;
        // No initial test for bitfields or layout
        assert p_saved1.v == 0;
        assert p_saved2.v == 4;

        assert saved_identity_g1 == cast(initial.g1, object);
        assert saved_identity_g2_2 == cast(initial.g2[2], object);
        assert saved_identity_g2_0_child
            == cast(initial.g2[0].child, object);
        assert saved_identity_g2_1_children_2_4
            == cast(initial.g2[1].children[2][4], object);

        assert saved_identity_device == cast(dev, object);
        assert saved_identity_test == cast(test, object);

        assert saved_traitref_g1 == cast(initial.g1, t);
        assert saved_traitref_g2_2 == cast(initial.g2[2], t);
        assert saved_traitref_g2_0_child == cast(initial.g2[0].child, t);
        assert saved_traitref_g2_1_children_2_4
            == cast(initial.g2[1].children[2][4], t);
        assert saved_traitref_g1.p == 1;
        assert saved_traitref_g2_2.p == 4;
        assert saved_traitref_g2_0_child.p == 5;
        assert saved_traitref_g2_1_children_2_4.p == 61;

        assert saved_hookref_g1_h1 == initial.g1.h1;
        assert saved_hookref_g2_2_h1 == initial.g2[2].h1;
        assert saved_hookref_g2_0_child_h1 == initial.g2[0].child.h1;
        assert saved_hookref_g2_1_children_2_4_h1 ==
            initial.g2[1].children[2][4].h1;
        assert saved_hookref_g1_h2 == initial.g1.h2;
        assert saved_hookref_g2_2_h2 == initial.g2[2].h2;
        assert saved_hookref_g2_0_child_h2 == initial.g2[0].child.h2;
        assert saved_hookref_g2_1_children_2_4_h2 ==
            initial.g2[1].children[2][4].h2;
    }
}

attribute test_later is write_only_attr {
    param type = "n";

    method set(attr_value_t _) throws {
        assert saved_int == 4;
        assert saved_int24 == -8388608;
        assert saved_bool == false;
        assert saved_float == -0.5;
        assert saved_size == 4;
        assert saved_long == -8388608;

        assert saved_struct.i == 4;
        assert saved_struct.v0.i == 0;
        assert saved_struct.v0.a[0] == -1;
        assert saved_struct.v0.a[1] == 2;
        assert saved_struct.v0.inner1.i == 3;
        assert saved_struct.v0.inner2.i == -2;

        for (local int i = 0; i < 4; i++) {
            assert saved_array[i] == 3-i;
            assert saved_byte_array_via_data[i] == 3-i;
            assert saved_byte_array_via_list[i] == 3-i;

            assert saved_signed_byte_array_via_data[i] == 1-i;
            assert saved_signed_byte_array_via_list[i] == 1-i;
        }

        for (local int i = 0; i < 4; i++) {
            assert port_array[i].v == i;
        }

        local const int matrix[4][3] =
            {{1, 5, 6},
             {8, -1, 10},
             {12, 0, 14},
             {0, 4, -8}};

        for (local int i = 0; i < 4; i++) {
            for (local int j = 0; j < 3; j++) {
                assert port_array[i].array_saved_int[j].v == matrix[i][j];
                assert b[i].r.f[j].v == matrix[i][j];
                assert saved_nested_array[i][j] == matrix[i][j];
                assert saved_nested_byte_array_via_data[i][j]
                    == cast(matrix[i][j], uint8);
                assert saved_nested_byte_array_via_list[i][j]
                    == cast(matrix[i][j], uint8);
                assert saved_nested_signed_byte_array_via_data[i][j]
                    == matrix[i][j];
                assert saved_nested_signed_byte_array_via_list[i][j]
                    == matrix[i][j];
            }
        }

        assert saved_int24_le == -1044736;
        assert saved_uint48_be == 0xF00F00F00;

        assert saved_bitfields == 0xF0F0FF;
        assert saved_layout.i32 == -5;
        assert saved_layout.il.i == 0xFF0FF00F0;
        assert saved_layout.b24 == 0xFF00F0;

        assert p_saved1.v == -5;
        assert p_saved2.v == 5;

        assert saved_identity_g1 == cast(g1, object);
        assert saved_identity_g2_2 == cast(g2[2], object);
        assert saved_identity_g2_0_child
            == cast(g2[0].child, object);
        assert saved_identity_g2_1_children_2_4
            == cast(g2[1].children[2][4], object);

        assert saved_identity_device == cast(dev, object);
        assert saved_identity_test == cast(test, object);

        assert saved_traitref_g1 == cast(g1, t);
        assert saved_traitref_g2_2 == cast(g2[2], t);
        assert saved_traitref_g2_0_child
            == cast(g2[0].child, t);
        assert saved_traitref_g2_1_children_2_4
            == cast(g2[1].children[2][4], t);

        assert saved_traitref_g1.p == -1;
        assert saved_traitref_g2_2.p == -4;
        assert saved_traitref_g2_0_child.p == -5;
        assert saved_traitref_g2_1_children_2_4.p == -61;

        assert saved_hookref_g1_h1 == g1.h1;
        assert saved_hookref_g2_2_h1 == g2[2].h1;
        assert saved_hookref_g2_0_child_h1 == g2[0].child.h1;
        assert saved_hookref_g2_1_children_2_4_h1 == g2[1].children[2][4].h1;
        assert saved_hookref_g1_h2 == g1.h2;
        assert saved_hookref_g2_2_h2 == g2[2].h2;
        assert saved_hookref_g2_0_child_h2 == g2[0].child.h2;
        assert saved_hookref_g2_1_children_2_4_h2 == g2[1].children[2][4].h2;
    }
}
